// Transform a matrix into a dense array.
func real[int, int] matrix2array(matrix M) {
  real[int, int] A(M.n, M.n);
  for (int i = 0; i < M.n; i++) {
    for (int k = 0; k < M.n; k++) {
      A(i, k) = M(i, k);
    }
  }
  return A;
}

// Computes the trace of an n times n matrix A.
func real tr(matrix A) {
  real trc = 0.0;
  for (int i = 0; i < A.n; i++) {
    trc += A(i, i);
  }
  return trc;
}

// Computes the gradient the scalar bound barrier function.
func real[int] gradbound(real[int] x, real bound) {
  real[int] dbound(x.n);
  for (int i = 0; i < x.n; i++) {
    dbound[i] = - 1.0 / ( x[i] - bound );
  }
  return dbound;
}

// Computes the gradient the SDP bound barrier function.
macro gradsdp(u, grad, Fx, Fxr) {
  matrix diffm = Fxr - Fx;
  real[int, int] diffa = matrix2array(diffm);
  real[int, int] diffainv = diffa ^ -1;
  matrix diffminv = diffainv;
  for (int m = mpirank; m < grad.n; m += mpisize) {
    matrix dm(u.n, u.n);
    matrix Bm = B[m];
    set(Bm, solver = sparsesolver);
    for (int k = 0; k < u.n; k++){
      for (int i = 0; i < u.n; i++){
        if ( i >= k ){
          real[int] temp = Bm * u[k][];
          real sv = u[i][]' * temp;
          dm(k, i) = sv;
        } else {
          dm(k, i) = dm(i, k);
        }
      }
    }
    matrix totrace = diffminv * dm;
    grad[m] = tr(totrace);
  }
  for (int m = 0; m < grad.n; m++ ) {
    int rnk = m % mpisize;
    broadcast(processor(rnk), grad[m]);
  }
} //
